# ╭──────────────────────────────────────────────────────────╮
# │ Shared System Configuration                              │
# ╰──────────────────────────────────────────────────────────╯
{
  config,
  flake,
  lib,
  pkgs,
  ...
}:
let
  inherit (flake) inputs;
  inherit (inputs) self;
  inherit (config) meta theme;

  nix-flake-update = pkgs.writeShellApplication {
    name = "nix-flake-update";
    text = ''nix flake update --commit-lock-file --flake ~/infrastructure "$@"'';
  };
in
{
  imports = [
    ./options.nix
    ./nushell.nix
  ];

  nixpkgs = {
    config.allowUnfree = true;
    overlays = lib.attrValues self.overlays;
  };

  nix =
    let
      flakeInputs = lib.filterAttrs (_: lib.isType "flake") inputs;
    in
    {
      settings = {
        experimental-features = [ "nix-command" "flakes" ];
        flake-registry = "";
        nix-path = config.nix.nixPath;
        trusted-users = [
          "root"
          (if pkgs.stdenv.hostPlatform.isDarwin then meta.username else "@wheel")
        ];
        auto-optimise-store = true;
      };
      channel.enable = false;
      registry = lib.mapAttrs (_: flake: { inherit flake; }) flakeInputs;
      nixPath = lib.mapAttrsToList (n: _: "${n}=flake:${n}") flakeInputs;
    };

  nixpkgs.config.permittedInsecurePackages = [
    "electron-39.8.10"
    "electron-40.10.5"
  ];

  environment.systemPackages = with pkgs; [
    curl
    wget

    duf
    ncdu
    openssl
    sops
    tree
    unzip

    nix-flake-update
    nix-prefetch-git
    nixfmt
  ];

  home-manager = {
    backupFileExtension = "backup";
    extraSpecialArgs = {
      inherit meta theme;
    };
  };

  users.users.${meta.username} = {
    inherit (meta) description;
    home = "/${if pkgs.stdenv.hostPlatform.isDarwin then "Users" else "home"}/${meta.username}";
  };

  programs = {
    gnupg.agent = {
      enable = true;
    }
    // lib.optionalAttrs (!pkgs.stdenv.hostPlatform.isDarwin) {
      settings = {
        default-cache-ttl = 86400;
        max-cache-ttl = 86400;
      };
    };
    zsh.enable = true;
  };

  time.timeZone = "Europe/Berlin";
}
